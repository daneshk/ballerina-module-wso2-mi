plugins {
    id 'java'
    id 'jacoco'
}

group = project.group
version = project.version

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    implementation "org.ballerinalang:ballerina-cli:${ballerinaLangVersion}"
    implementation "org.ballerinalang:ballerina-tools-api:${ballerinaLangVersion}"
    implementation "org.apache.ws.commons.axiom:axiom-api:${axiomVersion}"
    implementation "org.wso2.carbon.module:module-core:${carbonModuleCoreVersion}"
    implementation "org.apache.axis2.wso2:axis2:${axis2Version}"

    testImplementation 'org.testng:testng:7.7.0'
}

test {
    systemProperty "ballerina.home", "${project.rootDir}/target/ballerina-runtime"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    finalizedBy jacocoTestReport
}

test.dependsOn(":mi-ballerina:build")

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.destination file("${buildDir}/reports/jacoco/test/html")
    }

    // Include coverage from compiler-plugin module
    gradle.projectsEvaluated {
        def compilerPluginProject = project(':mi-compiler-plugin')

        // Add compiler-plugin classes to coverage
        additionalClassDirs.from = files(compilerPluginProject.sourceSets.main.output.classesDirs)
        additionalSourceDirs.from = files(compilerPluginProject.sourceSets.main.allSource.srcDirs)
    }
}
